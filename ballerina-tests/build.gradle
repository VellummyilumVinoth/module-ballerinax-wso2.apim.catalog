/*
 * Copyright (c) 2024, WSO2 LLC. (http://www.wso2.com) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'groovy'
}

description = 'WSO2 API manager server catalog module Integration Tests'

def packageName = "wso2.apim.catalog"
def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/BallerinaTest.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")
def ballerinaDist = "${project.rootDir}/target/ballerina-runtime"
def distributionBinPath =  "${ballerinaDist}/bin"

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-');
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

task updateTomlVerions {
    doLast {
        def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the native jar versions\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the native jar versions' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

def groupParams = ""
def disableGroups = ""
def debugParams = ""
def balJavaDebugParam = ""
def graalvmFlag = ""

task initializeVariables {
    if (project.hasProperty("groups")) {
        groupParams = "--groups ${project.findProperty("groups")}"
    }
    if (project.hasProperty("disable")) {
        disableGroups = "--disable-groups ${project.findProperty("disable")}"
    }
    if (project.hasProperty("debug")) {
        debugParams = "--debug ${project.findProperty("debug")}"
    }
    if (project.hasProperty("balJavaDebug")) {
        balJavaDebugParam = "BAL_JAVA_DEBUG=${project.findProperty("balJavaDebug")}"
    }
    if (project.hasProperty('balGraalVMTest')) {
        graalvmFlag = '--graalvm'
    }
}

def ballerinaPackagePath = "ballerina"

task publishTestResourcePackageToLocal {
    dependsOn("::${packageName}-ballerina:build")
    doLast {
        exec {
            workingDir "${project.rootDir}/${ballerinaPackagePath}"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "${distributionBinPath}/bal.bat pack && exit  %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "${distributionBinPath}/bal pack"
            }
        }

        exec {
            workingDir "${project.rootDir}/${ballerinaPackagePath}"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "${distributionBinPath}/bal.bat push --repository=local" +
                        " && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "${distributionBinPath}/bal push --repository=local"
            }
        }
    }
}

task startMockServices {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def dockerComposeFile = "$project.projectDir/resources/mock-services/compose.yml"
            def containerName = "wso2-apim-mock-services"

            // Check if container is already running
            def checkRunning = "docker ps --filter name=${containerName} --format {{.Names}}".execute()
            checkRunning.waitFor()
            def isRunning = checkRunning.text.trim()

            if (!isRunning) {
                println "Starting WSO2 APIM mock services container..."

                // Build and start the container
                exec {
                    commandLine 'sh', '-c', "docker compose -f ${dockerComposeFile} up -d --build"
                }

                // Wait for services to be healthy
                println "Waiting for mock services to start..."
                sleep(15000)

                // Verify health
                def healthCheck = "docker inspect --format='{{.State.Health.Status}}' ${containerName}".execute()
                healthCheck.waitFor()
                def health = healthCheck.text.trim()

                if (health == "healthy" || health == "") {
                    println "Mock services are ready"
                } else {
                    println "Warning: Container health status is ${health}"
                }
            } else {
                println "Mock services container is already running"
            }
        } else {
            println "Skipping Docker operations on Windows"
        }
    }
}

task stopMockServices {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def containerName = "wso2-apim-mock-services"

            // Check if container is running
            def checkRunning = "docker ps --filter name=${containerName} --format {{.Names}}".execute()
            checkRunning.waitFor()
            def isRunning = checkRunning.text.trim()

            if (isRunning) {
                println "Stopping WSO2 APIM mock services container..."

                exec {
                    commandLine 'sh', '-c', "docker stop ${containerName}"
                }

                // Wait for graceful shutdown
                sleep(5000)

                // Remove the container
                exec {
                    ignoreExitValue true
                    commandLine 'sh', '-c', "docker rm ${containerName}"
                }

                println "Mock services stopped"
            } else {
                println "Mock services container is not running"
            }
        } else {
            println "Skipping Docker operations on Windows"
        }
    }
}

task ballerinaTest {
    dependsOn(":${packageName}-ballerina:build")
    dependsOn(updateTomlVerions)
    dependsOn(initializeVariables)
    dependsOn(publishTestResourcePackageToLocal)
    dependsOn(startMockServices)
    finalizedBy(stopMockServices)
    finalizedBy(commitTomlFiles)

    doLast {
        exec {
            workingDir project.projectDir
            environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "${balJavaDebugParam} ${distributionBinPath}/bal.bat test ${graalvmFlag} ${groupParams} ${disableGroups} ${debugParams} && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "${balJavaDebugParam} ${distributionBinPath}/bal test ${graalvmFlag} ${groupParams} ${disableGroups} ${debugParams}"
            }
        }
    }
}

test {
    dependsOn(ballerinaTest)
}

build.dependsOn(":${packageName}-ballerina:build")
